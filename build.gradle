plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '3.1.1'
    id 'org.jetbrains.kotlin.jvm' version '2.1.0' // Use the latest version of Kotlin
    id 'com.gradleup.shadow' version '8.3.6'
}

group 'com.app'
version '0.1.0'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.10.2'
}



tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'com.app.demo'
    mainClass = 'com.app.demo.HelloApplication'
}
java {
    toolchain{
        languageVersion.set(JavaLanguageVersion.of(23))
    }
    sourceCompatibility = '23'
    targetCompatibility = '23'
}

javafx {
    version = '23.0.1'
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.web', 'javafx.swing', 'javafx.media']
}

dependencies {
    implementation('org.controlsfx:controlsfx:11.2.1')
    implementation('com.dlsc.formsfx:formsfx-core:11.6.0') {
        exclude(group: 'org.openjfx')
    }
    implementation('net.synedra:validatorfx:0.5.0') {
        exclude(group: 'org.openjfx')
    }
    implementation('org.kordamp.ikonli:ikonli-javafx:12.3.1')
    implementation('org.kordamp.bootstrapfx:bootstrapfx-core:0.4.0')
    implementation('eu.hansolo:tilesfx:21.0.3') {
        exclude(group: 'org.openjfx')
    }
    implementation('com.github.almasb:fxgl:17.3') {
        exclude(group: 'org.openjfx')
        exclude(group: 'org.jetbrains.kotlin')
    }
    implementation 'org.xerial:sqlite-jdbc:3.47.2.0'

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()
}

shadowJar{
    mergeServiceFiles()
    manifest{
        attributes 'Main-Class': 'com.app.demo.HelloApplication'
    }
}

jlink {

    // Set the name of the runtime image
    imageName = 'FBLA Project'

    launcher {
        name = 'FBLA Project'
    }
    //Merge non modular dependency into the modular package
    forceMerge 'sqlite-jdbc'

    // Configure jpackage to create an installer
    jpackage {

        installerOptions = [
                //no admin required
                '--win-per-user-install',
                //Add to start menu
                '--win-menu',
                //Makes fancy installer wizard
                '--win-dir-chooser',
                //for convenience
                '--verbose',
        ]

    }
}


jlinkZip {
    group = 'distribution'
}